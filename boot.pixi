include "constants.txt"
include "functions"

x = 0
y = 0
vx = 0.0
vy = 0.0
pvy = 0.0
HPXSIZE = PXSIZE / 2
HPYSIZE = PYSIZE / 2
state = new( 3, 1, INT8 )
state[ 0 ] = 0
state[ 1 ] = 0
state[ 2 ] = 0

pixel_size = get_pixel_size()
fit_screen_size()

mainloop:

while( get_event() ) {
    evt_type = EVT[ EVT_TYPE ]
    if evt_type == EVT_QUIT { halt }
    
    if evt_type == EVT_BUTTONDOWN { // Если нажата какая-нибудь клавиша
        if EVT[ EVT_KEY ] == 97 { // Если A
            state[ 0 ] = 1
        }
        
        if EVT[ EVT_KEY ] == 100 { // Если D
            state[ 1 ] = 1
        }
        
        if EVT[ EVT_KEY ] == 32 && abs(vy) < 0.1 && abs(pvy) < 0.1 { // Если пробел и если мы стоим на земле
            jump()
        }
        
        if EVT[ EVT_KEY ] == 156 || EVT[ EVT_KEY ] == 99 { // Если Shift или C
            energy_plus()
        }
    }
    
    if evt_type == EVT_BUTTONUP { // Если какая-нибудь клавиша отпущена
        if EVT[ EVT_KEY ] == 97 { // Если A
            state[ 0 ] = 0
        }
        
        if EVT[ EVT_KEY ] == 100 { // Если D
            state[ 1 ] = 0
        }
    }
    
    if evt_type == EVT_MOUSEBUTTONDOWN { // То же управление, но для телефонов
        if EVT[ EVT_Y ] > hysize / 2 {
            if EVT[ EVT_X ] > 0 {
                if abs( vy ) < 0.1 && abs( pvy ) < 0.1 {
                    jump()
                }
            } else {
                energy_plus()
            }
        }
        
        if EVT[ EVT_Y ] < hysize / 2 {
            if EVT[ EVT_X ] < 0 {
                state[ 0 ] = 1
            } else {
                state[ 1 ] = 1
            }
        }
    }
    
    if evt_type == EVT_MOUSEBUTTONUP {
        state[ 0 ] = 0
        state[ 1 ] = 0
    }
}

if state[ 0 ] { // Влево
    vx - 0.5
    
    if state[ 2 ] {
        vx = state[ 2 ] * ( -DASH )
        state[ 2 ] = 0
    }
}

if state[ 1 ] { // Вправо
    vx + 0.5
    
    if state[ 2 ] {
        vx = state[ 2 ] * DASH
        state[ 2 ] = 0
    }
}

pvy = vy
vy + GRAVITY
x + vx
y + vy

if y > hysize - HPYSIZE - 5 { // Пол
    y = hysize - HPYSIZE - 5
    vx * FLOORFRICTIONFACTOR
    if vy > 15 { // "Joke" - при сильном столкновении с полом персонажа...

        if PXSIZE < 50 {
            PXSIZE = 50 // ...растягивает...
        }

        if PYSIZE > 5 {
            PYSIZE = 5 // ...и сплющивает
            y + ( HPYSIZE - 2.5 )
        }
        
        HPXSIZE = PXSIZE / 2
        HPYSIZE = PYSIZE / 2
    }
    vy = 0.0
} else { 
    vx * AIRRESISTANCEFACTOR 
}

if x < - hxsize + HPXSIZE + 5 { x = -hxsize + HPXSIZE + 5 vx = vx * ( -WALLBOUNCINESS ) } // Левая стена
if x > hxsize - HPXSIZE - 5 { x = hxsize - HPXSIZE - 5 vx = vx * ( -WALLBOUNCINESS ) } // Правая стена

if WINDOW_XSIZE != xsize || WINDOW_YSIZE != ysize { fit_screen_size() }
draw()

go mainloop